// src/decoder.veryl

// support for
// ADD, ADDI, SUB, SLT, SLTI, AND, OR, LW, SW
//
// [31..26]     [5..0]              sub  op alu_b_sel s_ext rf_we dm_we  w_reg_sel d_sel
// -------------------------------- ---  -- --------- ----- ----- -----  --------- -----
// 00_1100 (0x0c)              ANDI  x   00     1       0     1     0         1      0
// 00_1101 (0x0d)              ORI   x   01     1       0     1     0         1      0
// 00_1000 (0x08)              ADDI  0   10     1       1     1     0         1      0
// 00_1010 (0x0a)              SLTI  1   11     1       1     1     0         1      0
// 10_0011 (0x23)              LW    0   10     1       1     1     0         1      1
// 10_1011 (0x2b)              SW    0   10     1       1     x     1         1      x
// 00_0100 (0x04)              BEQ   x   xx     x       1     0     0         x      x
// 00_0101 (0x05)              BNE   x   xx     x       1     0     0         x      x
//
// 000000       10_0100 (0x24) AND   x   00     0       x     1     0         0      0
// 000000       10_0101 (0x25) OR    x   01     0       x     1     0         0      0
// 000000       10_0000 (0x20) ADD   0   10     0       x     1     0         0      0
// 000000       10_0010 (0x22) SUB   1   10     0       x     1     0         0      0
// 000000       10_1010 (0x2a) SLT   1   11     0       x     1     0         0      0

// sub AluSub
// 0 Additon
// 1 Subtraction

// op AluOp
// 00 AND
// 01 OR
// 10 Arith
// 11 Neg

// alu_b_sel AluSource
// 0 reg b
// 1 imm

// s_ext immediate
// 0 zero extend
// 1 sign extend

// rf_we register file write enable
// 0 disable
// 1 enable

// dm_we data memory write enable
// 0 disable
// 1 enable

// w_reg_sel RegDestination
// 0 rt
// 1 rd

// d_sel alu or data datat memory
// 0 alu
// 1 dm

// pc_sel address to pc_register
// 00 pc + 4
// 01 jr register
// 10 relative branch
// 11 j immediate

module Decoder (
    i_opcode      : input  logic<6>,
    i_funct       : input  logic<6>,
    i_z           : input  logic   ,
    i_eq          : input  logic   ,
    o_w_reg_sel   : output logic   ,
    o_rf_we       : output logic   ,
    o_imm_sign_ext: output logic   ,
    o_alu_b_sel   : output logic   ,
    o_alu_sub     : output logic   ,
    o_alu_op      : output logic<2>,
    o_dm_we       : output logic   ,
    o_d_sel       : output logic   ,
    o_pc_sel      : output logic<2>,
) {
    always_comb {

        case i_opcode {
            // I type instructions
            //          andi (0c)
            6'b00_1100: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 1;
                            o_imm_sign_ext = 0;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b00; // and
                            o_dm_we        = 0;
                            o_d_sel        = 0;
                            o_pc_sel       = 2'b00; // pc + 4
                        }
                        // ori (0d)
            6'b00_1101: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 1;
                            o_imm_sign_ext = 0;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b01; // or
                            o_dm_we        = 0;
                            o_d_sel        = 0;
                            o_pc_sel       = 2'b00; // pc + 4
                        }
                        // addi (08)
            6'b00_1000: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 1;
                            o_imm_sign_ext = 1;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b10; // arith
                            o_dm_we        = 0;
                            o_d_sel        = 0;
                            o_pc_sel       = 2'b00; // pc + 4
                        }
                        // slti (0a)
            6'b00_1010: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 1;
                            o_imm_sign_ext = 1;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 1;
                            o_alu_op       = 2'b11; // slt
                            o_dm_we        = 0;
                            o_d_sel        = 0;
                            o_pc_sel       = 2'b00; // pc + 4
                        }
                        // lw (23)
            6'b10_0011: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 1;
                            o_imm_sign_ext = 1;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b10; // arith
                            o_dm_we        = 0;
                            o_d_sel        = 1; // selects data memory
                            o_pc_sel       = 2'b00; // pc + 4
                        }
                        // sw (2b)
            6'b10_1011: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 0; // no write to register file
                            o_imm_sign_ext = 1;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b10; // arith
                            o_dm_we        = 1;
                            o_d_sel        = 0; // don't care
                            o_pc_sel       = 2'b00; // pc + 4
                        }
                        // beq (04)
            6'b00_0100: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 0; // no write to register file
                            o_imm_sign_ext = 1;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b10; // arith
                            o_dm_we        = 1;
                            o_d_sel        = 0; // don't care
                            o_pc_sel       = if i_eq ? 2'b10 : 2'b00; // branch else pc + 4
                        }
            6'b00_0101: {
                            o_w_reg_sel    = 0; // rt
                            o_rf_we        = 0; // no write to register file
                            o_imm_sign_ext = 1;
                            o_alu_b_sel    = 1;
                            o_alu_sub      = 0;
                            o_alu_op       = 2'b10; // arith
                            o_dm_we        = 1;
                            o_d_sel        = 0; // don't care
                            o_pc_sel       = if i_eq ? 2'b00 : 2'b10; // pc + 4 else branch
                        }
                        // R-type
            6'b00_0000: {
                            o_w_reg_sel    = 1;
                            o_rf_we        = 1;
                            o_alu_b_sel    = 0;
                            o_imm_sign_ext = 0; // don't care
                            o_dm_we        = 0; // no write to data memory
                            o_d_sel        = 0; // alu result
                            o_alu_sub      = i_funct[1];
                            o_pc_sel       = 2'b00; // pc + 4
                            case i_funct {
                                // and
                                6'b10_0100: {
                                    o_alu_op = 2'b00;
                                }
                                // or
                                6'b10_0101: {
                                    o_alu_op = 2'b01;
                                }
                                // add
                                6'b10_0000: {
                                    o_alu_op = 2'b10;
                                }
                                // sub
                                6'b10_0010: {
                                    o_alu_op = 2'b10;
                                }
                                // slt
                                6'b10_1010: {
                                    o_alu_op = 2'b11;
                                }

                                default: {
                                 o_rf_we     = 0;
                                 o_alu_b_sel = 0; // don't care
                                 o_alu_sub   = 0; // don't care
                                 o_alu_op    = 2'b00; // don't care
                             }
                            }
                        }

            default: {
                         o_rf_we        = 0;
                         o_w_reg_sel    = 0; // don't care
                         o_imm_sign_ext = 0; // don't care
                         o_alu_b_sel    = 0; // don't care
                         o_alu_sub      = 0; // don't care
                         o_alu_op       = 2'b00; // don't care
                         o_dm_we        = 0;
                         o_d_sel        = 0; // don't care
                         o_pc_sel       = 2'b00; // pc + 4
                     }
        }
    }
}

#[test(decoder)]
embed (inline) sv{{{
    module test;
        logic [5:0] opcode;
        logic [5:0] funct;
        logic z;
        logic eq;
        logic reg_destination;
        logic write_enable;
        logic sign_extend;
        logic alu_source;
        logic alu_sub;
        logic [1:0] alu_op; 
        logic dm_we;
        logic rf_d_src;

        vips_Decoder decoder(
            opcode,
            funct,
            z,
            eq,
            reg_destination,
            write_enable,
            sign_extend,
            alu_source,
            alu_sub,
            alu_op,
            dm_we,
            rf_d_src,

        );

        initial begin
            // test I type
            // andi
            opcode = 6'b00_1100;
            #10;

            // ori
            opcode = 6'b00_1101;
            #10;

            // addi
            opcode = 6'b00_1000;
            #10;

            // slti 
            opcode = 6'b00_1010; 
            #10;

            // lw
            opcode = 6'b10_0011;
            #10;

            // sw
            opcode = 6'b10_1011;
            #10;

            // test R type
            opcode = 0;

            // and
            funct = 6'b10_0100;
            #10;

            // or
            funct = 6'b10_0101;
            #10;

            // add
            funct = 6'b10_0000;
            #10;

            // sub
            funct = 6'b10_0010;
            #10;
                                
            // slt
            funct = 6'b10_1010;
            #10;

            funct = 6'b10_1010;
            #10;

            // nor 
            opcode = 6'b00_0000;
            funct = 6'b10_0111;
            #10;

            // lui 
            opcode = 6'b11_1111;
            #10;

            $finish;
        end
    endmodule
}}}
